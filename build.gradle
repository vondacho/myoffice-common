buildscript {
    ext {
        springBootVersion = '2.0.0.RELEASE'
    }
    repositories {
        jcenter()
        mavenLocal()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.5'
        classpath 'io.franzbecker:gradle-lombok:1.10'
    }
}

allprojects {
    apply plugin: 'java'
    group 'edu.noia.myoffice'
    version = '0.0.14'
    repositories {
        jcenter()
        mavenLocal()
        maven {
            name 'mavenRemote'
            url myMavenRepoReadUrl
            credentials {
                username myMavenRepoReadUsername
                password myMavenRepoReadPassword
            }
        }
    }
}

subprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'maven-publish'
    apply plugin: 'org.sonarqube'
    apply plugin: 'io.franzbecker.gradle-lombok'
    sourceCompatibility = 1.8
    ext {
        lombokVersion = '1.16.18'
        mokitoVersion = '2.8.47'
        jacksonVersion = '2.9.2'
        h2DatabaseVersion = '1.4.196'
        hsqlDatabaseVersion = '2.4.0'
    }
    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        }
    }
    dependencies {
        compile("org.projectlombok:lombok:${lombokVersion}")
        testCompile("org.mockito:mockito-core:${mokitoVersion}")
    }
    jar {
        manifest.attributes provider: 'gradle'
    }
    publishing {
        repositories {
            mavenLocal()
            maven {
                name 'mavenRemote'
                url myMavenRepoWriteUrl
                credentials {
                    username myMavenRepoWriteUsername
                    password myMavenRepoWritePassword
                }
            }
        }
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
    lombok {
        version = "${lombokVersion}"
        sha256 = "" // skip verify Lombok task
    }
    task install(dependsOn: publishToMavenLocal)
    task deploy(dependsOn: publish)

    bootJar.enabled = false
    jar.enabled = true
}

gradle.taskGraph.whenReady { graph ->
    def badTasks = ['startScripts', 'distTar', 'distZip']
    graph.allTasks.findAll {it.name in badTasks}.each { task ->
        task.enabled = false
    }
}

project(':myoffice-common-domain') {
    dependencies {
        compile project(':myoffice-common-util')
    }
}

project(':myoffice-common-common') {
    dependencies {
        compile project(':myoffice-common-domain')
    }
}

project(':myoffice-common-event') {
    dependencies {
        compile project(':myoffice-common-domain')
    }
}

project(':myoffice-common-data') {
    dependencies {
        compile project(':myoffice-common-common')
        compile project(':myoffice-common-util')
        compile project(':myoffice-common-event')
    }
}

project(':myoffice-common-rest') {
    dependencies {
        compile project(':myoffice-common-common'),
                project(':myoffice-common-util')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2'
}